plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.16'
    id 'maven-publish'
}

intellij {
    sandboxDirectory "${ System.properties['user.home'] }/.idea-sandbox"
    type 'IC'
    version '2018.3'
}

compileJava {
    options.compilerArgs << '-parameters'
}

group 'saker.build'

if (project.findProperty("projVersion")?.trim()) {
    project.version = project.findProperty("projVersion")
} else {
    project.version = '0.8.12'
}


sourceCompatibility = 1.8

publishing {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
        }
    }
    pluginImpl {
        java {
            srcDirs = ['src/impl/java']
            compileClasspath += main.output
            compileClasspath += main.compileClasspath
        }
    }
}
jar {
    manifest {
        attributes (
            "Nest-Bundle-Format-Version" : "1",
            "Nest-Bundle-Identifier" : "saker.build.ide.intellij-v" +  version,
            "Nest-Bundle-Source": "saker.build.ide.intellij-sources-v" +  version
        )
    }
    dependsOn pluginImplClasses
    into('impl') {
        from sourceSets.pluginImpl.java.outputDir
        rename("(.*)", '$1_x')
    }
    archiveName("saker.build.ide.intellij-v" + version + ".jar")
}
task sourcesJar(type: Jar) {
    dependsOn patchPluginXml
    classifier = 'sources'
    from sourceSets.main.java.srcDirs
    from sourceSets.pluginImpl.java.srcDirs
    into 'META-INF', {
        from patchPluginXml.destinationDir
    }
    manifest {
        attributes (
            "Nest-Bundle-Format-Version" : "1",
            "Nest-Bundle-Identifier" : "saker.build.ide.intellij-sources-v" +  version
        )
    }
    archiveName("saker.build.ide.intellij-sources-v" + version + ".jar")
}

configurations {
    pluginImpl.extendsFrom compile
}

dependencies {
    pluginImplCompileOnly files('src/main/resources/saker.build.jar')
    pluginImplCompileOnly files('src/main/resources/saker.build-ide.jar')
    //testCompile group: 'junit', name: 'junit', version: '4.12'
}

patchPluginXml {
    untilBuild null
    sinceBuild null
}
verifyPlugin {
    ignoreWarnings = false
}